
# 0. Load parameters, settings etc

# Unpack config dictionary to local environment

# First part is a hack to recognize big samplesheets previously split
# We need to know the name of the sample-sheet to put (sub) results there
# These will be merged later
# import python module to get analysis name
import sys
from pathlib import Path

args = sys.argv
local_config_path = args[args.index("--configfile") + 2]  # relative path to local config file
# print(local_config_path)
# analysis_name is the root name of the yaml-file, without path
analysis_name = Path(local_config_path).stem

# As we use pathlib, we could have made a lot of the pathwork cooler
outputFolder = config['output_path'] + "/" + analysis_name  # create path to folder to put results in
data_type = config['data_type']
detection_pvalue = config['detection_pvalue']
bisulphite_conversion_rate = config['bisulphite_conversion_rate']
plate_data_dir = config['root_data_path'] + "/" + config['local_path']


# max sample size depends on 450k or EPIC, meaning the data type:
if data_type == "450k":
    max_sample_size = config['max_sample_size_450k']
elif data_type == "EPIC":
    max_sample_size = config['max_sample_size_epic']
else:
    print(f"Unknown chip/data_type {data_type}. Supportet EPIC and 450k")
    exit(1) 
        

# 0. check that the sample size is not larger than allowed sample size
# If it is, split it.
rule sample_size_check:
    input:
        script = "scripts/0_checkSampleSize.R",
        plates=plate_data_dir
    params:
        renv=config['renv'],
        analysis_name=analysis_name,
        max_sample_size=max_sample_size,
        folder=outputFolder
    output:
        sampleSheet=f'{outputFolder}/tmp_results/{analysis_name}_sampleSheet.rds'
    log:
        f"{outputFolder}/logs/0_checkSampleSize.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.plates} {params.analysis_name} {params.max_sample_size} {output.sampleSheet} {params.folder} &> {log}"

# 1. Build RGSet

rule build_rgset:
    input:
        script="scripts/1_buildRGSet.R",
        sheet=rules.sample_size_check.output.sampleSheet
    params:
        renv=config['renv']
    output:
        rgset=f'{outputFolder}/tmp_data/{analysis_name}_1_RGset.rds'
    log:
        f"{outputFolder}/logs/1_buildRGSet.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.sheet} {output.rgset} &> {log}"

# 2. Filter probes from RGSet
rule filter_probes_rgset:
    input:
        script="scripts/2_filterRGSet.R",
        rgset=rules.build_rgset.output.rgset,
        cross_hybridizing=f'input/cross_hybridizing_{data_type}.txt'
    params:
        filter_cross_hybridizing=config['filter_cross_hybridizing'],
        filter_polymorphic_CpGs=config['filter_polymorphic_CpGs'],
        detection_p=detection_pvalue,
        renv=config['renv']
    output:
        probes_removed=f'{outputFolder}/results/{analysis_name}_2_probes_removed.csv',
        rgset_filtered_probes=f'{outputFolder}/tmp_data/{analysis_name}_2_RGSet_filtered_probes.rds',
        SNPbetas=f'{outputFolder}/results/{analysis_name}_2_SNP_Betas.rds',
        pvalueMat=f'{outputFolder}/tmp_results/{analysis_name}_2_pvalueMat.rds'
    log:
        f"{outputFolder}/logs/2_filterRGSet.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.rgset} {input.cross_hybridizing} {params.filter_cross_hybridizing} {params.filter_polymorphic_CpGs} {params.detection_p} {output.probes_removed} {output.SNPbetas} {output.rgset_filtered_probes} {output.pvalueMat} &> {log}"


# 3. Add PCA plot as part of report

rule make_control_probe_PCA:
    input:
        script="scripts/3_makeControlProbePCA.R",
        rgset=rules.filter_probes_rgset.output.rgset_filtered_probes
    params:
        renv=config['renv'],
        boxplot_dir=f'{outputFolder}/sensitive_plots/3_boxplots'
    output:
        PCA_plot=f'{outputFolder}/plots/{analysis_name}_3_control_probe_PCA_plot.pdf', 
        control_probe_PCA=f'{outputFolder}/results/{analysis_name}_3_control_probe_PCA.rds',
        NA_control_probe=f'{outputFolder}/tmp_results/{analysis_name}_3_filtered_NA_control_probes.csv',
        boxplot_done=f'{outputFolder}/sensitive_plots/3_boxplots/{analysis_name}_3_boxplot_control_probes_done.txt'
    log:
        f"{outputFolder}/logs/3_makeControlProbePCA.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.rgset} {output.PCA_plot} {output.control_probe_PCA} {output.NA_control_probe} {params.boxplot_dir} {output.boxplot_done} &> {log}"


# 4. Filter out samples with less than 90% CpG probes detected 
qc_path1 = f'{outputFolder}/tmp_results/{analysis_name}_4'
qc_path2 = f'{outputFolder}/results/{analysis_name}_4'
qc_step1 = f'detectionP{detection_pvalue}'

rule filter_samples_rgset:
    input:
        script="scripts/4_dropSamples.R",
        rgset=rules.filter_probes_rgset.output.rgset_filtered_probes,
        NA_control_probes=rules.make_control_probe_PCA.output.NA_control_probe
    params:
        detection_pvalue=detection_pvalue,
        bisulphite_conversion_rate=bisulphite_conversion_rate,
        renv=config['renv'],
        poor_frac=config['poor_quality_frac']
    output:
        detectionPvalues=f'{qc_path1}_detection_pvalues.csv',
        probeQC=f'{qc_path2}_num_probes_with_proportion_failed_samples_p{detection_pvalue}.csv',
        sampleQC=f'{qc_path2}_probes_failed_per_sample_p{detection_pvalue}.csv',
        bisulphiteQC=f'{qc_path2}_bisulphite_conversions.csv',
        rgset_filtered=f'{outputFolder}/tmp_data/{analysis_name}_4_RGSet_filtered_probes_and_samples.rds',
        dropped_sample_sheet=f'{qc_path1}_dropped_samples.csv'
    log:
        f"{outputFolder}/logs/4_dropSamples.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.rgset} {input.NA_control_probes} {params.detection_pvalue} {params.bisulphite_conversion_rate} {params.poor_frac} {output.detectionPvalues} {output.probeQC} {output.sampleQC} {output.bisulphiteQC} {output.rgset_filtered} {output.dropped_sample_sheet} &> {log}"

# ---- 5. Preprocess RGChannelSet to MethylSet

# Read in relevant configuration
preprocess_methods = config['preprocess_methods']

rule preprocess_to_methylset:
    input:
        script="scripts/5_preprocessToMethylSet.R",
        rgset=rules.filter_samples_rgset.output.rgset_filtered
    params:
        preprocess_methods=preprocess_methods,
        renv=config['renv']
    output:
        methylsets=expand('{outputFolder}/tmp_data/{analysis_name}_5_MethylSet_{preprocess_method}.rds',
            analysis_name=analysis_name, preprocess_method=preprocess_methods, outputFolder=outputFolder)
    log:
        f"{outputFolder}/logs/5_preprocessToMethylSet.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.rgset} {params.preprocess_methods} {output.methylsets} &> {log}"

# ---- 6. Visual the Raw RGSet vs Each Preprocessing Method with QC Metrics

preprocess_string = '_vs_'.join(preprocess_methods)
comparisons = f'rgSet_vs_{preprocess_string}'

rule plot_density_preprocessed_vs_rgset:
    input:
        script="scripts/6_plotDensityPreprocessedVsRGSet.R", 
        methylsets=rules.preprocess_to_methylset.output.methylsets,
        rgset=rules.filter_samples_rgset.output.rgset_filtered
    params:
        plot_titles=preprocess_methods,
        renv=config['renv'],
        subset_size=config['subset_plotting'],
        plot_dir=f'{outputFolder}/sensitive_plots/6_density_plots'
    output:
        plots_done=f'{outputFolder}/sensitive_plots/6_density_plots/{analysis_name}_6_{comparisons}_density_plots_done.txt'
    log:
        f"{outputFolder}/logs/6_plotDensityPreprocessedVsRGSet.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.methylsets} {input.rgset} {params.plot_titles} {params.subset_size} {params.plot_dir} {output.plots_done} &> {log}"

selected_preprocess_method = config['selected_preprocess_method']
selected_methylset_file_name = f'{analysis_name}_5_MethylSet_{selected_preprocess_method}'
selected_methylset = f'{analysis_name}_7_MethylSet_{selected_preprocess_method}'

# ---- 7. Filter out samples that failed manual QC
discarded_samples = config['root_data_path'] + '/' + config['discarded_samples']

rule filter_samples_failed_manual_qc:
    input:
        script="scripts/7_filterSamplesManualQC.R",
        methylset=f'{outputFolder}/tmp_data/{selected_methylset_file_name}.rds',
        discarded_samples=discarded_samples,
        filtered_samples=rules.filter_samples_rgset.output.dropped_sample_sheet
    params:
        renv=config['renv']
    output:
        methylset=f'{outputFolder}/tmp_data/{selected_methylset}_filtered_manual_qc.rds',
        removed_samples=f'{outputFolder}/tmp_results/{analysis_name}_7_filtered_samples.csv'
    log:
        f"{outputFolder}/logs/7_filterSamplesManualQC.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.methylset} {input.discarded_samples} {input.filtered_samples} {output.methylset} {output.removed_samples} &> {log}"


# 8. Make QC plot for selected preprocessing method
rule make_qc_plot:
    input:
        script="scripts/8_makeQCPlot.R",
        methylset=rules.filter_samples_failed_manual_qc.output.methylset,
        filtered_samples=rules.filter_samples_failed_manual_qc.output.removed_samples
    params:
        renv=config['renv']
    output:
        qc_plot=f'{outputFolder}/plots/{analysis_name}_8_qc_plot.pdf',
        filtered_methset=f'{outputFolder}/tmp_data/{analysis_name}_8_filtered_methset.rds',
        filtered_sample_list=f'{outputFolder}/results/{analysis_name}_8_filtered_samples.csv'
    log:
        f"{outputFolder}/logs/8_makeQCPlot.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.methylset} {input.filtered_samples} {output.qc_plot} {output.filtered_methset} {output.filtered_sample_list} &> {log}"


# 9. Estimate cell type proportions

rule estimate_cell_type_proportions:
    input:
        script="scripts/9_estimateCellTypeProportions.R",
        rgset=rules.filter_samples_rgset.output.rgset_filtered
    params:
        cell_type=config['cell_type'],
        selected_preprocess_method=selected_preprocess_method,
        renv=config['renv']
    output:
        cellcounts=f'{outputFolder}/results/{analysis_name}_9_estimated_cell_proportions.csv'
    log:
        f"{outputFolder}/logs/9_estimateCellTypeProportions.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.rgset} {params.cell_type} {params.selected_preprocess_method} {output.cellcounts} &> {log}"


# ---- 10. Convert GenomicMethylSet to GenomicRatioSet and add annotations

rule convert_gmset_to_grset:
    input:
        script="scripts/10_convertGMSetToGRSet.R",
        methylset=rules.make_qc_plot.output.filtered_methset
    params:
        renv=config['renv']
    output:
        ratioset=f'{outputFolder}/tmp_data/{analysis_name}_10_{selected_preprocess_method}_GenomicRatioSet.rds'
    log:
        f'{outputFolder}/logs/10_convertGMSetToGRSet.Rout'
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.methylset} {output.ratioset} &> {log}"


# ---- 11. Sex prediction and removement of discordant gender/predicted sex samples
rule filter_discordant_sex_samples:
    input:
        script="scripts/11_predictSex.R",
        ratioset=rules.convert_gmset_to_grset.output.ratioset
    params:
        renv=config['renv']
    output:
        filtered_ratioset=f'{outputFolder}/tmp_data/{analysis_name}_11_{selected_preprocess_method}_GenomicRatioSet_filter_sex_disc.rds',
        added_pheno=f'{outputFolder}/results/{analysis_name}_11_added_sex_prediction_to_pheno.csv',
        sex_plot=f'{outputFolder}/plots/{analysis_name}_11_sex_plot.pdf'
    log:
        f"{outputFolder}/logs/11_predictSex.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.ratioset} {output.filtered_ratioset} {output.added_pheno} {output.sex_plot} &> {log}"        


# ---- 12. Separate sex chromosomes

rule separate_sex_chromosomes:
    input:
        script="scripts/12_separateSexChromosomesInGRSet.R",
        ratioset=rules.filter_discordant_sex_samples.output.filtered_ratioset,
        pvalMat=rules.filter_probes_rgset.output.pvalueMat
    params:
        renv=config['renv']
    output:
        ratioset_no_sex_chr=f'{outputFolder}/tmp_data/{analysis_name}_12_{selected_preprocess_method}_GenomicRatioSet_filter_probes_without_sex_chr.rds',
        beta_matrix_sex_chr=f'{outputFolder}/results/{analysis_name}_12_{selected_preprocess_method}_beta_matrix_sex_chr.csv',
        beta_matrix_sex_chr_rds=f'{outputFolder}/results/{analysis_name}_12_{selected_preprocess_method}_beta_matrix_sex_chr.rds',
        pvalMatNoSex=f'{outputFolder}/results/{analysis_name}_12_pvalueMat_autosomal.csv',
        pvalMatNoSexRds=f'{outputFolder}/results/{analysis_name}_12_pvalueMat_autosomal.rds',
        pvalMatSex=f'{outputFolder}/results/{analysis_name}_12_pvalueMat_sex_chr.csv',
        pvalMatSexRds=f'{outputFolder}/results/{analysis_name}_12_pvalueMat_sex_chr.rds'
    log:
        f'{outputFolder}/logs/12_separateSexChromosomesInGRSet.Rout'
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.ratioset} {input.pvalMat} {output.ratioset_no_sex_chr} {output.pvalMatNoSex} {output.pvalMatNoSexRds} {output.beta_matrix_sex_chr} {output.beta_matrix_sex_chr_rds} {output.pvalMatSex} {output.pvalMatSexRds} &> {log}"

# ---- 13. Do BMIQ on beta values from GenomicRatioSet

selected_data_type = config['data_type']

rule bmiq_on_grset:
    input:
        script="scripts/13_bmiqGRSet.R",
        ratioset_no_sex_chr=rules.separate_sex_chromosomes.output.ratioset_no_sex_chr
    params:
        subset=config['subset_BMIQ'],
        renv=config['renv']
    output:
        bmiqed_data=f'{outputFolder}/results/{analysis_name}_13_bmiqed_beta_values.csv',
        bmiqed_data_rds=f'{outputFolder}/results/{analysis_name}_13_bmiqed_beta_values.rds',
        raw_data=f'{outputFolder}/results/{analysis_name}_13_beta_values.csv',
        raw_data_rds=f'{outputFolder}/results/{analysis_name}_13_beta_values.rds'
    log:
        f'{outputFolder}/logs/13_bmiqGRSet.Rout'
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.ratioset_no_sex_chr} {params.subset} {output.bmiqed_data} {output.bmiqed_data_rds} {output.raw_data} {output.raw_data_rds} &> {log}"


# ---- 14. Plot density after BMIQ
rule plot_density_after_bmiq:
    input:
        script="scripts/14_plotDensityAfterBMIQ.R",
        methylsets=rules.preprocess_to_methylset.output.methylsets,
        rgset=rules.filter_samples_rgset.output.rgset_filtered,
        bmiqed_data=rules.bmiq_on_grset.output.bmiqed_data_rds
    params:
        plot_titles=preprocess_methods,
        subset=config['subset_plotting'],
        renv=config['renv'],
        plot_dir=f'{outputFolder}/sensitive_plots/14_density_plots'
    output:
        plots_done=f'{outputFolder}/sensitive_plots/14_density_plots/{analysis_name}_14_BMIQ_density_plots_done.txt'
    log:
        f"{outputFolder}/logs/14_plotDensityAfterBMIQ.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.methylsets} {input.rgset} {input.bmiqed_data} {params.plot_titles} {params.subset} {params.plot_dir} {output.plots_done} &> {log}"

# ---- 15. Plot histogram of differences
rule plot_histogram_of_differences:
    input:
        script="scripts/15_histogram_of_differences.R",
        rgset=rules.filter_samples_rgset.output.rgset_filtered,
        methset=rules.preprocess_to_methylset.output.methylsets,
        bmiqed=rules.bmiq_on_grset.output.bmiqed_data_rds
    params:
        subset=config['subset_plotting'],
        renv=config['renv']
    output:
        plot=f'{outputFolder}/plots/{analysis_name}_15_histogram_of_differences.pdf'
    log:
        f"{outputFolder}/logs/15_histogram_of_differences.Rout"
    shell:
        "Rscript --vanilla {input.script} {params.renv} {input.rgset} {input.methset} {input.bmiqed} {params.subset} {output.plot} &> {log}"

# ---- 16. Clean up - remove intermediate data
rule clean_up:
    input:
        script="scripts/16_clean_up.sh",
        folder=f'{outputFolder}'
    log:
        f"{outputFolder}/logs/16_clean_up.log"
    shell:
        "bash {input.script} {input.folder} &> {log}"


rule first_part:
    input:
        f'{outputFolder}/plots/{analysis_name}_3_control_probe_PCA_plot.pdf',
        f'{outputFolder}/results/{analysis_name}_3_control_probe_PCA.rds',
        f'{outputFolder}/sensitive_plots/3_boxplots/{analysis_name}_3_boxplot_control_probes_done.txt'


rule second_part:
    input:
        f'{outputFolder}/sensitive_plots/6_density_plots/{analysis_name}_6_{comparisons}_density_plots_done.txt'


rule third_part:
    input:
        f'{outputFolder}/sensitive_plots/6_density_plots/{analysis_name}_6_{comparisons}_density_plots_done.txt',
        f'{outputFolder}/plots/{analysis_name}_8_qc_plot.pdf',
        f'{outputFolder}/results/{analysis_name}_9_estimated_cell_proportions.csv',
        f'{outputFolder}/sensitive_plots/14_density_plots/{analysis_name}_14_BMIQ_density_plots_done.txt',
        f'{outputFolder}/plots/{analysis_name}_15_histogram_of_differences.pdf'

