##
#
# This snakefile executes the QC pipeline. The different parts of the pipeline are organized in modules represented in different snake files.
# The pipeline is expected to run in the conda environment named 'snpQc' built using `auxiliaries/environment/snpQc-env.txt`.
#
# Commands to execute from the qc-pipeline/snakefiles folder:
# conda activate snp_qc
# snakemake --snakefile Snakefile --cores 16 --use-conda --rerun-incomplete
#
##

# External libraries
from pathlib import Path
from shutil import copyfile
import pandas as pd
import numpy as np
import plotnine as p9
import matplotlib
import math
import sys
import yaml
import io
import os
matplotlib.use('Agg')
from shutil import copyfile


# Config file
configfile: "parameters/config.yaml"

# Local libraries
sys.path.append('utils')  # in order to find our local packages
import mobaQcTools as mqc

# Pipeline variables and setup
include: "parameters/variables.py"
include: "parameters/batch_details.py"
include: "utils/setup.py"

# Ensure bash as shell
shell.executable("/bin/bash")

# A file with rule properties and ordering
with open("parameters/rules.yaml", 'r') as stream:
    rule_info = yaml.safe_load(stream)

# A bit of sanity check
for batch in batches:
    if batch not in batch_genotyping_chip:
        raise Exception(f"Batch '{batch}' not in 'batch_genotyping_chip', please edit 'batch_details.py' accordingly.")
    if batch not in batch_name:
        raise Exception(f"Batch '{batch}' not in 'batch_mobagenetics_10_name', please edit 'batch_details.py' accordingly.")

# Report (top level)
report: "report/qc.rst"

# The different modules
include: "mod1-data-conversion.snake"
include: "mod2-genetic-relationship.snake"
include: "mod3-population-clustering.snake"
include: "mod4-good-markers.snake"
include: "mod5-pre-phasing.snake"
include: "mod6-imputation.snake"
include: "mod7-post-imputation.snake"
include: "mod8-release_annotation.snake"


# rule all, lists the output of every module
rule all:
    input:
        # Module 1:
        mod1_start_het = expand(tmpMod1/"{batch}"/'mod1_start_het.het', batch = batches),
        mod1_start_hwe = expand(tmpMod1/"{batch}"/'mod1_start_hwe.hwe', batch = batches),
        mod1_start_freq = expand(tmpMod1/"{batch}"/'mod1_start_freq.frq', batch = batches),
        mod1_start_lmiss = expand(tmpMod1/"{batch}"/'mod1_start_missing.lmiss', batch = batches),
        mod1_start_imiss = expand(tmpMod1/"{batch}"/'mod1_start_missing.imiss', batch = batches),
        mod1_end_bed_file = expand(tmpMod1 / "{batch}" / "m1_output.bed", batch = batches),
        mod1_end_bim_file = expand(tmpMod1 / "{batch}" / "m1_output.bim", batch = batches),
        mod1_end_fam_file = expand(tmpMod1 / "{batch}" / "m1_output.fam", batch = batches),

        # Module 2:
        mod2_start_het= expand(tmpMod2 / "{batch}" / 'mod2_start_het.het', batch = batches),
        mod2_start_hwe= expand(tmpMod2 / "{batch}" / 'mod2_start_hwe.hwe', batch = batches),
        mod2_start_freq= expand(tmpMod2 / "{batch}" / 'mod2_start_freq.frq', batch = batches),
        mod2_start_lmiss= expand(tmpMod2 / "{batch}" / 'mod2_start_missing.lmiss', batch = batches),
        mod2_start_imiss= expand(tmpMod2 / "{batch}" / 'mod2_start_missing.imiss', batch = batches),
        mod2_end_bed_file = expand(tmpMod2 / "{batch}" / "m2_output.bed", batch = batches),
        mod2_end_bim_file = expand(tmpMod2 / "{batch}" / "m2_output.bim", batch = batches),
        mod2_end_fam_file = expand(tmpMod2 / "{batch}" / "m2_output.fam", batch = batches),
        mod2_end_pruned_bed_file = expand(tmpMod2 / "{batch}" / "m2_output_pruned.bed", batch = batches),
        mod2_end_pruned_bim_file = expand(tmpMod2 / "{batch}" / "m2_output_pruned.bim", batch = batches),
        mod2_end_pruned_fam_file = expand(tmpMod2 / "{batch}" / "m2_output_pruned.fam", batch = batches),
        pruned_snps = expand(tmpMod2/"{batch}"/"m2_output.prune.in", batch = batches),
        sex_check = expand(tmpMod2/"{batch}"/"m2_output.sexcheck", batch = batches),
        king_relatedness = expand(tmpMod2/"{batch}"/"m2_output.kin0", batch = batches),
        fam_docs = expand(github_docs/"{batch}"/"fam_reconstruction.md", batch = batches),
        mismatch_information = tmpMod2/"mismatch_information.gz",
        mismatch_relationship = tmpMod2/"mismatch_relationship.gz",

        # Module 3:
        mod3_start_het = expand(tmpMod3 / "{batch}" / 'mod3_start_het.het', batch = batches),
        mod3_start_hwe = expand(tmpMod3 / "{batch}" / 'mod3_start_hwe.hwe', batch = batches),
        mod3_start_freq = expand(tmpMod3 / "{batch}" / 'mod3_start_freq.frq', batch = batches),
        mod3_start_lmiss = expand(tmpMod3 / "{batch}" / 'mod3_start_missing.lmiss', batch = batches),
        mod3_start_imiss = expand(tmpMod3 / "{batch}" / 'mod3_start_missing.imiss', batch = batches),
        mod3_end_pcs = expand(tmpMod3/"{batch}"/"pca_both.pcs", batch = batches),
        mod3_end_eigenvector = expand(tmpMod3/"{batch}"/"pca_both.vec", batch = batches),
        mod3_end_snp_loadings = expand(tmpMod3/"{batch}"/"pca_both.load", batch = batches),
        mod3_end_eigenvalues = expand(tmpMod3/"{batch}"/"pca_both.val", batch = batches),
        mod3_end_variance_explained = expand(tmpMod3/"{batch}"/"pca_both.pve", batch = batches),
        mod3_end_cluster_file = expand(tmpMod3/"{batch}"/"clusters", batch = batches),
        mod3_end_ceu_file = expand(tmpMod3/"{batch}"/"ceu_core_ids", batch = batches),
        mod3_md_file_1kg = expand(github_docs / "{batch}" / "pca_1kg_moba.md", batch = batches),

        # Module 4:
        m4_output_good_markers = expand(tmpMod4/ "{batch}" / "m4_output_good_markers.bed", batch = batches),

        # Module 8:
        m8_sex_check = tmpMod8/"check_sex.sexcheck"


